plugins {
    id("java")
    id("org.jetbrains.intellij.platform") version "2.0.1"
    id("pl.allegro.tech.build.axion-release") version "1.18.7"
}

group = "com.github.imvs"

scmVersion {
    ignoreUncommittedChanges.set(false)
}

project.version = scmVersion.version

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

intellijPlatform {
    pluginVerification {
        ides {
            recommended()
        }
    }
}

java {
    sourceCompatibility = "17"
    targetCompatibility = "17"
}

tasks {
    signPlugin {
        certificateChain.set(project.property("signPlugin.certificateChain").toString())
        privateKey.set(project.property("signPlugin.privateKey").toString())
        password.set(project.property("signPlugin.password").toString())
    }

    publishPlugin {
        token.set(project.property("publishPlugin.publishToken").toString())
    }

    patchPluginXml {
        sinceBuild = provider { "232.10300" }
        untilBuild = provider { null }
    }
}

dependencies {

    intellijPlatform {
        intellijIdeaCommunity("2023.2.6")
        bundledPlugin("com.intellij.java")
        instrumentationTools()
        pluginVerifier()
        zipSigner()
    }

    implementation("org.springframework.cloud:spring-cloud-config-server:4.1.0") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-web"
        exclude group: "org.springframework.boot", module: "spring-boot-starter-validation"
        exclude group: "org.springframework.security", module: "spring-security-crypto"
        exclude group: "ORG.APACHE.SSHD", module: ""
        exclude group: "org.slf4j", module: ""
    }
    compileOnly files("libs/forms_rt.jar")
    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"
    testCompileOnly "org.projectlombok:lombok:1.18.32"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.32"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.7.2"
}

test {
    useJUnitPlatform()
}

